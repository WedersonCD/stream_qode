<html>

<head>

    <title>Stream Qode</title>

	<script src="../../resources/assets/external/requirejs/require.js"></script>
    <script src='./js/utils.js'></script>

    <script src='./js/qlik.js'></script>
    <!--<script src='./js/block.js'></script>-->
    
    <link rel="stylesheet" href="./css/style.css">
    <link rel="stylesheet" href="./css/block.css">
    
    <script type="text/hyperscript">
        init 
        repeat while no $QLIK
            js
                return QLIK
            end
            set $QLIK to it        
            wait 0.5s
        end

        def getCurrentAppLink
            set currentAppId to .header-appSelecion-select's @app-id

            set extension to ''

            make a URL from '/sense/app/'+currentAppId+extension+'/overview',$QLIK.host
                return it.href
        end
        
        def createAnLine(leftBlock,rightBlock)
            js(leftBlock,rightBlock)
                return new LeaderLine(leftBlock,rightBlock)
            end then
            return it
        end

        def getUnicId
            js
                return UTILS.get_unic_id()
            end then
            return it
        end

        def setBlockIDDepply(el,blockId)
        
            tell el
                set @block-id to blockId
        
            tell <div/> in el
                set @block-id to blockId
        end
        
        def setNewBlockIDDepply(el,blockId)
            get getUnicId() then
                put it into blockId then
                call setBlockIDDepply(el,blockId)
        end

        def getBlockEditor(blockId)
            js(blockId)
                return ace.edit('editor-'+blockId)
            end then 
            return it
        end

        def getElementByClassAndAttribute(c,attribute,value)
            js(c,attribute,value)
                return document.body.querySelector(c+"["+attribute+"='"+value+"']")
            end then
            return it
        end

        def removeBlock(blockId)
            get getElementByClassAndAttribute('.stream-block','block-id',blockId) then remove it

            send remove to <div.stream-block-code-miniature[block-id='${blockId}']/>
        end

        def selectBlock(blockId)

            add .display-none to .stream-block
            get getElementByClassAndAttribute('.stream-block','block-id',blockId) then 
                remove .display-none from it

            remove .selected from .stream-block-code-miniature
            get getElementByClassAndAttribute('.stream-block-code-miniature','block-id',blockId) then 
                add .selected to it
        end

        def getOptionObject(option)
            set optionName to option's name
            fetch './blocks/stream_block_code_options.json' as Object then put the result into options
            return options.filter( \ item -> item.label == optionName)
        end

        def getBlockCode(block,blockId)
            set blockId to block's @block-id  
            get getBlockEditor(blockId) then put it into blockEditor
            set checkedOptions to <:checked/> in block
            
            set code to blockEditor.getValue()
            set preCode to ''
            set posCode to ''
            for checkedOption in checkedOptions
                
                get getOptionObject(checkedOption) then put it into option
                if option.pre_code then set preCode to preCode+'\n'+ option.pre_code end
                if option.pos_code then set posCode to posCode+'\n'+ option.pos_code end

            end
            
            return preCode+'\n'+code+'\n'+posCode

        end

        def getFullCode
            set codeBlocks to .stream-block
            set fullCode to ''
            for codeBlock in codeBlocks
                set blockId to codeBlock's @block-id
                set codeBlockName to .stream-block-header-name-label-value in codeBlock
                get getBlockCode(codeBlock) then put it into code
                set code to '\n///$tab '+codeBlockName's textContent+'\n\n' + code +'\n'
                set fullCode to fullCode+code
            end
            return fullCode
        end

    </script>
</head>

<body>
    <div class="outfocus display-none popup" _="
        on click
             toggle .display-none on .popup end
        ">

    </div>
    <div class="select-template display-none popup" _="
    init 
        fetch ./template_files.json as Object put result into templates
        for template in templates
            make an <li.select-template-list-item.pointer/>
                put template.name into it
                tell it
                    set @file to template.file

                put it at the end of .select-template-list
        end

    end
    ">
            <h4>Code Templates</h4>
            <ul class="select-template-list clean-on-close" _="
            on click 
                set fileName to target's @file
                set blockId to first .select-template's @block-id
                get getBlockEditor(blockId) then put it into editor
                fetch `./code_templates/${fileName}` put result into code
                editor.setValue(code) then             
                toggle .display-none on .popup
            end"></ul>
    </div>
    <section class="header">
        <div class="header-addBlock pointer">
            <button class="header-addBlock-add pointer" _="
            on click
                get getUnicId() then put it into blockId

                make an <div/> then put it into setupBlockDiv
                    fetch ./blocks/stream_block_code_setup.html as Fragment then put the result into setupBlockDiv
                    call setBlockIDDepply(setupBlockDiv,blockId)
                    put .stream-block in setupBlockDiv at the end of .content-blockSetup

                make an <div/> then put it into miniatureBlockDiv
                    fetch ./blocks/stream_block_code_miniature.html as Fragment then put the result into miniatureBlockDiv
                    call setBlockIDDepply(miniatureBlockDiv,blockId)
                    put .stream-block-code-miniature in miniatureBlockDiv at the end of .content-blockView

                call selectBlock(blockId)
            end
            ">+</button>
            â†“
        </div>
        <div class="header-logo">
            Stream Qode
        </div>
        <div class="header-appSelecion">
            <label class="header-appSelecion-label" for="header-appSelecion-select">App:</label>
            <select class="header-appSelecion-select" _="
            init fetch ./template_apps.json as json then
                for app in result
                    make an <option/> then                        
                        put app.name into it
                        put it at end of me
                        tell it
                            set @app-id to app.id
                end

                tell my options[0]
                    put @app-id into my @app-id
                end
            end
            
            on change
                tell my options[I.selectedIndex]
                    put @app-id into my @app-id
            end
            ">
            </select>
        </div>
        <div  class="header-run">
            <div class="header-run-icon" _="
            on click
                add .display-none to .header-run-play-icon
                remove .display-none from .header-run-loading-icon

                log 'getting the full code...'
                get getFullCode() put it into fullCode
                log 'getting opening current app...'
                set currentAppId to .header-appSelecion-select's @app-id
                set currentApp to $QLIK.qlik.openApp(currentAppId[0])
                log 'getting setting code app...'
                call currentApp.setScript(fullCode)
                call currentApp.doSave()
                log 'reloading app...'
                call currentApp.doReload()
                log 'finished!!'

                add .display-none to .header-run-loading-icon
                remove .display-none from .header-run-play-icon

                

            end">
                <img class="header-run-play-icon pointer" src="./assets/icons/run.svg" />
                <img class="header-run-loading-icon rotate display-none" src="./assets/icons/loading.svg" />
            </div>
        </div>
        <div class="header-openApp pointer" _="
            on click
                get getCurrentAppLink() then
                go to url `${it}` in new window
            end
                ">
            <img class="header-openApp-open-icon" src="./assets/icons/open.svg" />
        </div>
    </section>
    <section class="content">
        <section class="content-blockSetup">
        </section>
        <section class="content-blockView">
        </section>

    </section>

    <!--libs-->
    <script src='./js/libs/_hyperscript.min.js'></script>
    <script src='./js/libs/plain-draggable.min.js'></script>
    <script src='./js/libs/leader-line.min.js'></script>
    <script src='./js/libs/ace.js'></script>
    <script>
        //remove Qlik CSS
        new Promise(r => setTimeout(()=>document.querySelector("head > link:nth-child(18)").remove(), 500));
        
    </script>
</body>

</html>